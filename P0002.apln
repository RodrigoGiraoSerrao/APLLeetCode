:Namespace P0002
    ⍝ LeetCode problem #2                                      
    ⍝ Add Two Numbers, https://leetcode.com/problems/add-two-numbers/
    (⎕IO ⎕ML) ← 0 1
       
    Sol ← {
        n ← ⍺⌈⍥≢⍵             
        a ← n↑⍺ ⋄ w←n↑⍵  ⍝ Make sure both numbers have same number of digits.
        CU ← {  ⍝ Update the carry of a temporary result
        ⍝ e.g. 10 9 8 → 0 10 8 0 → 0 0 9 0 0                
            cs←0,⌊10÷⍨⍵     
            cs+10|⍵,0       
        }
        ⍝ Do the carry updates and remove trailing 0s                   
        v⌿⍨~⌽∧⍀⌽0=v ← CU⍣n⊢a+w
    }   
                                
    ⍝ "Obvious" solution uses decode but is probably against spirit of the problem.
    SolDec ← ⌽⍤(10⊥⍣¯1+⍥(10∘⊥⌽))
:EndNamespace
